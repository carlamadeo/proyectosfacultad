
external boolean actual_parameters = True;

/* Some constants for the Notes and Items we'll add */
const string DATATYPENOTE = "DataType";
const string DATATYPEITEM = "DataType";

/* 
    This script is for 4.2D users of SCE who are migrating to 5.0.
    It copies the parameter's name to its datatype annotation so
    that 4.2D diagrams will still pass 5.0's improved checks.
*/

void
main()
{
    string  query;
    cntx    the_cntx;

    /* All the parameters in the system will have a DataType annotation since
    	it is required.  Find the ones will null values. */
    query = "cntx[cntx_refs[file[SceDiagram]] && " + 
    	    "(DataIn || DataOut || DataInOut || ControlIn || ControlOut) && "+
    	    "(notes[DataType && items[DataType && value == null]] || !notes[DataType])]";
    id_list_create(query, "all_params");

    /* Now get all formal parameters */
    query = "cntx[all_params && link[from_node[FormalCaller]]]";
    for_each_in_select(query, the_cntx)
    {
    	/* copy the name value to the DataType */
   	MakeDataTypeAnnot(the_cntx, the_cntx.name);
    }

    if (actual_parameters)
    {
        /* Then get all actual parameters and copy their names
	   to their datatypes */
        query = "cntx[all_params && link[from_node[!FormalCaller] && to_node[!DataModule]]]";
    	for_each_in_select(query, the_cntx)
    	{
    	    /* copy the name value to the DataType */
    	    MakeDataTypeAnnot(the_cntx, the_cntx.name);
    	}
    }

    /* Get any parameters to Global data and copy each's name value to 
    	its datatype */
    query = "cntx[all_params && link[to_node[DataModule]]]";
    for_each_in_select(query, the_cntx)
    {
    	/* copy the name value to the DataType */
        MakeDataTypeAnnot(the_cntx, the_cntx.name);
    }
    id_list_free("all_params");
    system("sysbuildrep -s " + current_system() + " -p " + current_projdir());
}

void 
MakeDataTypeAnnot(cntx the_cntx, string datatype)
{
    string command;

    /* Annot the cntx object */
    command  = "stpem -s " + current_system() + " -p " + current_projdir() 
    	    	+ " -ed ngoae -C 'AnnotEdit " + the_cntx.id + "'";
    /* Select the DataType note */
    command = command + " -C 'NoteSelect " + DATATYPENOTE + "'";
    /* Seelct the DataType item */
    command = command + " -C 'ItemSelect " + DATATYPEITEM + "'";
    /* Set its value */
    command = command + " -C 'EditorSetValue \"" + datatype + "\"'";
    /* Save the file */
    command = command + " -C 'FileSave' -C 'AnnotUnload'";
    system(command);
}
