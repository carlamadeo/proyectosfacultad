#include "val/qrl/include/table.inc"

  const int width_1 = 200;
  const int width_2 = 400;
  const int width_3 = 300;
  const int width_4 = 300;

  const string coltitle1 = "Requirement ID";
  const string coltitle2 = "Short Description";
  const string coltitle3 = "Source Document";
  const string coltitle4 = "Source Rqt ID";



void
main ()
{
  print_message ("Start Report Generation");

  format ("ValidatorDocuments.rtf");

  string caption;

  string q = "file [REQTable] sort by name";
  list filelist = list_select(q);
  file filevar;
  int i;
  int count = list_count(filelist);

  for (i=0;i<count;i++)
    {
      filevar = list_get(filelist,i);
      paragraph ("Heading 3");
      print ("Requirement Table: " + filevar.name);

      caption = filevar.name + " Requirements Traceability Table";
      PrintTraceabilityTable (filevar.id, filevar.name, caption);
    }
}

void
PrintTraceabilityTable (int id, string name, string caption)
{
  string c1,c2,c3,c4;

  string q = "node [REQRequirement && in_links[REQCurrentRequirementIdentifier] && node_refs[file[${id}]]]";

  list nodelist = list_select(q);
  node nodevar;

  int i;
  int count = list_count(nodelist);

  InitializeTable();


  for (i=0;i<count;i++)
    {
      nodevar = list_get(nodelist,i);
      
      c1 = nodevar.name;
      c2 = GetItem("REQInformalNameItem", nodevar.id);
      c3 = GetItem("RequirementDocument", nodevar.id);
      c4 = GetItem("RequirementParagraph", nodevar.id);

      PrintTableRow(c1, c2, c3, c4);
    }


  End_Table();

  print_table(name, caption);

  print_message (name + " Table Printed");


}

string
GetItem(string typename, int id)
{
   string q = "item[${typename} && obj_id==${id}]";

   item itemvar = find_by_query (q);

   if (itemvar == NULL)
     return ("unspecified");
   else
     {
       if (itemvar.value == "")
         return ("unspecified");
       else
         return (itemvar.value);
     }
}

void
PrintTableRow( string c1, string c2, string c3, string c4 )
{

      Begin_Row();

      Make_Cell(width_1, NULL, NULL, NULL, NULL, NULL, c1 );
      Make_Cell(width_2, NULL, NULL, NULL, NULL, NULL, c2 );
      Make_Cell(width_3, NULL, NULL, NULL, NULL, NULL, c3 );
      Make_Cell(width_4, NULL, NULL, NULL, NULL, NULL, c4 );

      End_Row();

}



void 
InitializeTable()
{


   table_caption_placement_set(Top);
   table_width_set(6.5);

   table_caption_paragraph_format_set ("TableTitle");
   table_cell_bold_paragraph_format_set ("CellBody");
   table_cell_paragraph_format_set ("CellBodyleft");


   // create title bar

   Begin_Table(4);

   Begin_Row();

   Make_Cell(width_1, NULL, "Shade0", NULL, NULL, NULL, coltitle1);
   Make_Cell(width_2, NULL, "Shade0", NULL, NULL, NULL, coltitle2);
   Make_Cell(width_3, NULL, "Shade0", NULL, NULL, NULL, coltitle3);
   Make_Cell(width_4, NULL, "Shade0", NULL, NULL, NULL, coltitle4);

   End_Row();

}




